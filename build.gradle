apply plugin: 'java'
apply plugin: "com.github.sherter.google-java-format"
apply plugin: 'application'

mainClassName = findProperty("main") ?: "com.williamfiset.algorithms.${findProperty("algorithm") ?: 'missingPackage.missingClass'}"

// https://github.com/sherter/google-java-format-gradle-plugin
buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "gradle.plugin.com.github.sherter.google-java-format:google-java-format-gradle-plugin:0.8"
  }
}

// Assume Java 8
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  
  // JUnit framework
  testCompile 'junit:junit:4.+'
  compile 'junit:junit:4.+'

  // Apache commons lang
  compile 'org.apache.commons:commons-lang3:3.6'

  // JUnit 5 / Jupiter
  testImplementation('org.junit.jupiter:junit-jupiter-api:5.4.2')
  testRuntime('org.junit.jupiter:junit-jupiter-engine:5.4.2')
   
  // Google Guava lib
  testCompile group: 'com.google.guava', name: 'guava', version: '22.0'

  // Google truth API
  // https://mvnrepository.com/artifact/com.google.truth/truth
  testCompile group: 'com.google.truth', name: 'truth', version: '1.0'

  // Test mocking framework
  testCompile "org.mockito:mockito-core:1.+"
}

test {
  dependsOn cleanTest
  testLogging.showStandardStreams = true
}

tasks.withType(JavaCompile) {
  dependsOn(installGitHooks)
  options.compilerArgs << '-Xlint:unchecked'
  options.deprecation = true
}

// Options when compiling tests
compileTestJava {
  options.encoding = 'UTF-8'
  options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

task buildDependenciesFolder(type: Copy) {
  from configurations.compile
  into './dependencies'
}

// Google Java Format pre-commit hook installation
// Stolen from: https://github.com/uber/NullAway/commit/8f06f3878fe450bce19c448d48591d77255f2aa0
// Allows running the pre-commit hook file under config/hooks/pre-commit

tasks.register('installGitHooks', Copy) {
  from(file('config/hooks/pre-commit-stub')) {
    rename 'pre-commit-stub', 'pre-commit'
  }
  into file('.git/hooks')
  fileMode 0777
}
