buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
}

plugins {
  // https://github.com/diffplug/spotless
  id("com.diffplug.spotless") version "6.25.0"
}

apply plugin: 'java'
apply plugin: 'application'

application {
  mainClass = findProperty("main") ?: "com.williamfiset.algorithms.${findProperty("algorithm") ?: 'missingPackage.missingClass'}"
}

repositories {
    maven { url 'https://maven.google.com' }
    gradlePluginPortal()
    mavenCentral()
}

// Describe all project deps. Use 'testCompile' for test dependencies and
// 'compile' for project dependencies.
dependencies {
  // Apache commons lang
  testImplementation 'org.apache.commons:commons-lang3:3.17.0'

  // JUnit 5 / Jupiter
  testImplementation('org.junit.jupiter:junit-jupiter:5.10.0')
  testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.10.0')

  // Google Guava lib
  testImplementation group: 'com.google.guava', name: 'guava', version: '33.5.0-jre'

  // Google Truth test framework
  // https://mvnrepository.com/artifact/com.google.truth/truth
  testImplementation group: 'com.google.truth', name: 'truth', version: '1.1.5'

  // Test mocking framework
  testImplementation "org.mockito:mockito-core:5.+"

  // JUnit Platform Launcher to run tests from the command line
  testRuntimeOnly('org.junit.platform:junit-platform-launcher:1.10.0')
}

spotless {
  java {
    googleJavaFormat('1.19.1')
  }
}

test {
  dependsOn cleanTest
  testLogging.showStandardStreams = true
}

tasks.named('test', Test) {
  useJUnitPlatform()
}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
  options.compilerArgs += [
    '-Xlint:unchecked',
    '-Xlint:deprecation'
  ]
}

// Options when compiling tests
compileTestJava {
  options.encoding = 'UTF-8'
  options.compilerArgs += [
    '-Xlint:unchecked',
    '-Xlint:deprecation'
  ]
}

task buildDependenciesFolder(type: Copy) {
  from configurations.implementation
  into './dependencies'
}
